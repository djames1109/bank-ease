plugins {
    id 'org.springframework.boot' version '3.4.1' apply(false)
    id 'io.spring.dependency-management' version '1.1.7' apply(false)
    id 'org.sonarqube' version '6.0.1.5171' apply(false)
    id 'org.castle.djames.bankease.arch-rules-enforcer' version '1.0.0-SNAPSHOT' apply(false)
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    if (project.path == ':components:common'
            || project.path == ':services:user-service') {

        apply plugin: 'java'
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
        apply plugin: 'jacoco'
        apply plugin: 'org.sonarqube'
        apply plugin: 'org.castle.djames.bankease.arch-rules-enforcer'

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
        }

        jacoco {
            toolVersion = "0.8.12"
        }

        sonarqube {
            properties {
                property "sonar.projectKey", "$sonarProjectKey"
                property "sonar.organization", "$sonarProjectName"
                property "sonar.host.url", "$sonarHostUrl" // or your SonarQube server URL
                property "sonar.login", "$sonarLogin"

                // Pass the path to the aggregated XML report
                property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml"
            }
        }

        dependencies {
            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.projectlombok:lombok'
            testCompileOnly 'org.projectlombok:lombok'
            testAnnotationProcessor 'org.projectlombok:lombok'
        }
    }
}

